# System call numbers are here:
# http://mdfs.net/Docs/Comp/PDP11/Unix/SYSCalls

const FCB_BUFFER_SIZE := 512;
typedef FCBIndexType is uint16;

record RawFCB is
	fd: uint16;
end record;


include "fileio.coh";

@impl sub FCBRawRead is
	var fd := fcb.fd;
	var dma := &fcb.buffer[0];
    var i: int16;

    @asm "mov #0087h, -(sp)"; # ret
    @asm "mov #0, -(sp)"; # whence
    @asm "mov ", pos, "+0, -(sp)"; # pos lo
    @asm "mov ", pos, "+2, -(sp)"; # pos hi
    @asm "mov #8913h, -(sp)"; # lseek
    @asm "mov ", fd, ", r0";
    @asm "call @sp";
    @asm "add #8, sp";

    @asm "mov ", len, ", -(sp)";
    @asm "mov ", dma, ", -(sp)";
    @asm "mov #8903h, -(sp)"; # read
    @asm "mov ", fd, ", r0";
    @asm "call @sp";
    @asm "add #8, sp";
    @asm "mov r0,", i;

    if i < 0 then
		amount := 0;
		fcb.flags := fcb.flags | FCB_FLAG_ERROR;
	else
		amount := i as FCBIndexType;
	end if;
end sub;

@impl sub FCBRawWrite is
	var fd := fcb.fd;
	var dma := &fcb.buffer[0];
    var i: int16;

    @asm "mov #0087h, -(sp)"; # ret
    @asm "mov #0, -(sp)"; # whence
    @asm "mov ", pos, "+0, -(sp)"; # pos lo
    @asm "mov ", pos, "+2, -(sp)"; # pos hi
    @asm "mov #8913h, -(sp)"; # lseek
    @asm "mov ", fd, ", r0";
    @asm "call @sp";
    @asm "add #8, sp";

    @asm "mov ", len, ", -(sp)";
    @asm "mov ", dma, ", -(sp)";
    @asm "mov #8904h, -(sp)"; # write
    @asm "mov ", fd, ", r0";
    @asm "call @sp";
    @asm "add #8, sp";

    if i < 0 then
        fcb.flags := fcb.flags | FCB_FLAG_ERROR;
    end if;
end sub;

sub fcb_i_open(fcb: [FCB], filename: [uint8], flags: uint32): (errno: uint8) is
    _fcb_init(fcb);

    @asm "mov #0087h, -(sp)"; # ret
    @asm "mov #666o, -(sp)";
    @asm "mov ", filename, ", -(sp)";
    @asm "mov #8905h, -(sp)"; # trap 5
    @asm "call @sp";
    @asm "add #8, sp";

	var fd: int16;
    @asm "mov r0, ", fd;
	fcb.fd := fd as uint16;
	if fd < 0 then
		errno := (-fd) as uint8;
	else
		errno := 0;
	end if;
end sub;

sub FCBOpenIn(fcb: [FCB], filename: [uint8]): (errno: uint8) is
	(errno) := fcb_i_open(fcb, filename, 0o0000); # O_RDONLY
end sub;

sub FCBOpenUp(fcb: [FCB], filename: [uint8]): (errno: uint8) is
	(errno) := fcb_i_open(fcb, filename, 0o0002); # O_RDWR
end sub;

sub FCBOpenOut(fcb: [FCB], filename: [uint8]): (errno: uint8) is
	(errno) := fcb_i_open(fcb, filename, 0o1102); # O_RDWR|O_CREAT|O_TRUNC
end sub;
	
sub FCBClose(fcb: [FCB]): (errno: uint8) is
	FCBFlush(fcb);

	var fd := fcb.fd;
    @asm "mov ", fd, ", r0";
    @asm "trap 6";
    @asm "neg r0";
    @asm "mov r0, ", errno;
end sub;

sub FCBExt(fcb: [FCB]): (len: uint32) is
	FCBFlush(fcb);

	var fd := fcb.fd;
    @asm "mov ", fd, ", r0";
    @asm "trap 19";
    @asm "dw 0, 0"; # pos
    @asm "dw 2"; # whence
    @asm "mov r0, ", len, "+2";
    @asm "mov r1, ", len, "+0";
end sub;

include "common-file.coh";

# vim: ts=4 sw=4 et
